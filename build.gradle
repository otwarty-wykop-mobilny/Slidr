import org.jetbrains.kotlin.gradle.plugin.KotlinPluginWrapperKt
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    alias(libs.plugins.starter.config)
    alias(libs.plugins.starter.versioning)
    alias(libs.plugins.starter.library.android) apply false
}

commonConfig {
    javaFilesAllowed false
    javaVersion JavaVersion.VERSION_11
}

allprojects {
    pluginManager.withPlugin("kotlin") { plugin ->
        def kotlinVersion = KotlinPluginWrapperKt.getKotlinPluginVersion(project)
        configurations.configureEach {
            resolutionStrategy.eachDependency {
                if (requested.group == 'org.jetbrains.kotlin' && requested.name.startsWith("kotlin")) {
                    useVersion kotlinVersion
                }
            }
        }
    }

    pluginManager.withPlugin("java") {
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(JavaVersion.VERSION_17.toString())
            }
        }
    }

    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            apiVersion = "1.5"
            languageVersion = "1.5"
        }
    }
}
